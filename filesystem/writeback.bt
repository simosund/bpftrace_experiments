#!/usr/bin/env bpftrace

BEGIN
{
  printf("Tracing writeback... Hit Ctrl-C to end\n");
  printf("%-9s %-8s %-8s %-16s %s\n", "TIME", "DEVICE", "PAGES", "REASSON", "ms");

  //see /ses/kernel/debug/tracing/events/writeback/writeback_start/format
  @reason[0] = "background";
  @reason[1] = "vmscan";
  @reason[2] = "sync";
  @reason[3] = "periodic";
  @reason[4] = "laptop_timer";
  @reason[5] = "free_more_memory";
  @reason[6] = "fs_free_space";
  @reason[7] = "forker_thread";
}

tracepoint:writeback:writeback_start
{
  @start[args->sb_dev] = nsecs;
  @pages[args->sb_dev] = args->nr_pages;
}

tracepoint:writeback:writeback_written
/@start[args->sb_dev]/
{
  $sb_dev = args->sb_dev;
  $s = @start[$sb_dev];
  $lat = $s ? (nsecs - $s) / 1000 : 0;
  $pages = @pages[args->sb_dev] - args->nr_pages;

  time("%H:%M:%S  ");
  printf("%-8s %-8d %-16s %d.%03d\n", args->name, $pages, @reason[args->reason], $lat / 1000, $lat % 1000);

  delete(@start[$sb_dev]);
  delete(@pages[$sb_dev]);
}

END
{
  clear(@reason);
  clear(@start);
}
